require conf/distro/poky.conf

DISTRO = "mel"
DISTRO_NAME = "Mentor Embedded Linux"
DISTRO_VERSION = "5.0+snapshot-${DATE}"
MAINTAINER = "Mentor Graphics Corporation <support@mentor.com>"
SDK_VENDOR = "-melsdk"
TARGET_VENDOR = "-mel"

# Application Development Environment
ADE_PROVIDER = "Mentor Graphics Corporation"
ADE_SITENAME = "Application Development Environment for ${DISTRO_NAME}"
ADE_SECTIONS = "devel bootloaders kernel source sdk"
ADE_SECTIONS_EXCLUDED = "locale"

require conf/distro/include/mel-versions.conf
require conf/distro/include/mel-providers.conf
require conf/distro/include/mel-vardeps.conf

WARN_QA = "ldflags useless-rpaths rpaths unsafe-references-in-binaries unsafe-references-in-scripts staticdev"
ERROR_QA = "dev-so debug-deps dev-deps debug-files arch la2 pkgconfig la perms"

# Ensure we really default to not building 32 bit pseudo on 64 bit machines
NO32LIBS ?= "1"

# Mask out the TI bringup image, as it requires meta-angstrom
BBMASK = "/recipes-misc/images/"

# Temporary, this is in oe-core now
BBMASK .= "|/meta-oe/recipes-kernel/kmod/"

# Mask out systemd bits, as we use sysvinit
BBMASK .= "|/meta-efl/recipes-efl/efl/"
BBMASK .= "|/meta-gnome/recipes-gnome/gdm/"
BBMASK .= "|/meta-oe/meta-oe/recipes-extended/polkit/"
BBMASK .= "|/meta-oe/recipes-bsp/keymaps/"
BBMASK .= "|/meta-oe/recipes-connectivity/cherokee/"
BBMASK .= "|/meta-oe/recipes-connectivity/connman/"
BBMASK .= "|/meta-oe/recipes-connectivity/dhcp/"
BBMASK .= "|/meta-oe/recipes-connectivity/gateone/"
BBMASK .= "|/meta-oe/recipes-connectivity/networkmanager/"
BBMASK .= "|/meta-oe/recipes-connectivity/openssh/"
BBMASK .= "|/meta-oe/recipes-core/busybox/"
BBMASK .= "|/meta-oe/recipes-core/dropbear/"
BBMASK .= "|/meta-oe/recipes-core/systemd/"
BBMASK .= "|/meta-oe/recipes-devtools/cloud9/"
BBMASK .= "|/meta-oe/recipes-extended/atftp/"
BBMASK .= "|/meta-oe/recipes-extended/cronie/"
BBMASK .= "|/meta-oe/recipes-extended/lighttpd/"
BBMASK .= "|/meta-oe/recipes-extended/rsyslog/"
BBMASK .= "|/meta-oe/recipes-graphics/xinput-calibrator/"
BBMASK .= "|/meta-oe/recipes-graphics/xserver-nodm-init/"
BBMASK .= "|/meta-oe/recipes-navigation/gpsd/"
BBMASK .= "|/meta-oe/recipes-support/ntp/"
BBMASK .= "|/meta-oe/recipes-support/syslog-ng/"
BBMASK .= "|/meta-oe/recipes-support/udisks/"

# We know RHEL 5 is the oldest distro we support, and that binaries built
# there will run on any other host we support
SSTATE_MIRROR_HOSTS = "RedHatEnterprise-5"

# Ensure that we implement shared state reuse handling for non-target recipes
require conf/distro/include/sstate.inc

# Ensure the emitted locale packages are in that section, so they can be
# easily excluded from the ADE (or categorized correctly, when included)
LOCALE_SECTION = "locale"

EXTRAOPKGCONFIG = ""

# Disable automatic ccache usage for more deterministic builds
CCACHE = ""

# Prefer SCM to external mirrors
PREMIRRORS = ""

# Relocation fixups
export GUILE_LOAD_PATH = "${STAGING_DATADIR_NATIVE}/guile/1.8"

# Ensure pkg-config can find native .pc files as well as the host, even when
# pkg-config has been relocated.
PKG_CONFIG_PATH = "${STAGING_LIBDIR}/pkgconfig:${STAGING_DATADIR}/pkgconfig"
PKG_CONFIG_PATH .= ":${STAGING_LIBDIR_NATIVE}/pkgconfig:${STAGING_DATADIR_NATIVE}/pkgconfig"

# A problem we have is that while the info page for ld says that sysv is the
# default in RHEL5, it is actually not and it defaults to gnu hash.  This
# in turn results in binaries that won't run on older systems.  We change to
# forcing both hash types to be included to fix this issue.
BUILD_LDFLAGS += "-Wl,--hash-style=both"

QA_LOG = "1"

# we need the u-boot image types available
IMAGE_CLASSES ?= "image_types_uboot"

# Default to these image types
IMAGE_FSTYPES ?= "tar.bz2 ext3"
UBI_VOLNAME = "rootfs"

# Quadruple the normal. 'du' is not a good way to really see how much
# space will be needed and fails badly as the fs size grows.
IMAGE_ROOTFS_EXTRA_SPACE = "40960"

DISTRO_FEATURES = "alsa argp bluetooth ext2 irda pcmcia usbgadget usbhost wifi xattr \
                   nfs zeroconf pci 3g ${DISTRO_FEATURES_LIBC}"

# Add nls if we're supporting.
DISTRO_FEATURES += "${@['','nls'][bb.data.getVar('USE_NLS', d, 1) == 'yes']}"

# Do not add largefile for mips64 since it causes problems (e.g. in busybox/df)
# Special largefile syscalls (e.g. statfs64) are not implemented in 64bit kernel
DISTRO_FEATURES += "${@['largefile',''][bb.data.getVar('TARGET_ARCH', d, 1) == 'mips64']}"

# Needed bits for our knobs (e.g. package group definitions)
# Note: include this after the DISTRO_FEATURES bits, as it adds to it
require conf/distro/include/knobs.inc

# ensure the panda machine from the pandaboard-rework branch has a kernel
COMPATIBLE_MACHINE_append_pn-linux-omap4 = "|pandaboard"

# Make this match the target machine, to save space.
PREFERRED_VERSION_linux-libc-headers-native ??= "${KVERSION}"

# Turn off debug information on host-run utils
BUILD_OPTIMIZATION += "-g0"

# Targets for qemu
QEMU_TARGETS ?= "arm i386 mips mipsel mips64 mips64el ppc x86_64"

# We prefer busybox rather than tinylogin
VIRTUAL-RUNTIME_login_manager = "busybox"

# We want information about image contents
INHERIT += "buildhistory"

# We want information about task executions
INHERIT += "buildstats"
INHERIT += "buildstats-summary"

# Ensure our external toolchain is sane
require conf/distro/include/toolchain-sanity.inc

# Add symlink to ${KERNEL_IMGTYPE}
require conf/distro/include/kernel-link.inc

# Force early expansion of recursing package vars
PACKAGE_CLASSES = "package_ipk package_early_expand"

# More sane usability for the archiver classes
ARCHIVER_MODE ?= "none"
ARCHIVER_MODE[type] = "choice"
ARCHIVER_MODE[choices] = "none original patched configured"
ARCHIVER_CLASS = "${@'archive-${ARCHIVER_MODE}-source' \
                     if ARCHIVER_MODE != 'none' else ''}"
INHERIT += "${ARCHIVER_CLASS}"

# Ensure we have license-filtered sources available
COPYLEFT_LICENSE_INCLUDE = '*'
COPYLEFT_LICENSE_EXCLUDE = 'CLOSED Proprietary Mentor INTEL'
INHERIT += "copyleft_compliance"
